"=============================================================================
" vimrc--- The real vimrc
" => Yang Yang
"=============================================================================

"---------------------------------------------------------------------------
" profile start profile.txt
" profile file ~/.vim/rc/*
" profile func dein#*

if &compatible
  set nocompatible
endif

let g:Config_Main_Home = fnamemodify(expand('<sfile>'),
      \ ':p:h:gs?\\?'.((has('win16') || has('win32')
      \ || has('win64'))?'\':'/') . '?')

let g:kiooss_rc_home = g:Config_Main_Home

function! s:source_rc_simple(path, ...) abort "{{{
  let abspath = resolve(expand('~/.vim/rc/' . a:path))
  execute 'source' fnameescape(abspath)
endfunction "}}}

function! s:source_rc(path, ...) abort "{{{
  let use_global = get(a:000, 0, !has('vim_starting'))
  let abspath = resolve(expand(g:kiooss_rc_home . '/' . a:path))
  if !use_global
    execute 'source' fnameescape(abspath)
    return
  endif

  " substitute all 'set' to 'setglobal'
  let content = map(readfile(abspath),
        \ 'substitute(v:val, "^\\W*\\zsset\\ze\\W", "setglobal", "")')
  " create tempfile and source the tempfile
  let tempfile = tempname()
  try
    call writefile(content, tempfile)
    execute 'source' fnameescape(tempfile)
  finally
    if filereadable(tempfile)
      call delete(tempfile)
    endif
  endtry
endfunction "}}}

augroup MyAutoCmd
  autocmd!
  autocmd FileType,Syntax,BufNewFile,BufNew,BufRead *?
        \ call vimrc#on_filetype()
  autocmd CursorHold *.toml syntax sync minlines=300
augroup END

if has('vim_starting')
  call s:source_rc('init.rc.vim')
endif

call s:source_rc('dein.rc.vim')

if has('vim_starting') && !empty(argv())
  call vimrc#on_filetype()
endif

if !has('vim_starting')
  call dein#call_hook('source')
  call dein#call_hook('post_source')

  syntax enable
  filetype plugin indent on
endif

"---------------------------------------------------------------------------
call s:source_rc('options.rc.vim')
call s:source_rc('colorscheme.rc.vim')
call s:source_rc('mappings.rc.vim')
call s:source_rc('abbreviations.rc.vim')
call s:source_rc('autocmds.rc.vim')
call s:source_rc('mix.rc.vim')

if has('nvim')
  call s:source_rc('neovim.rc.vim')
endif

" source plugin's config
" for s:path in split(glob('~/.vim/rc/plugins.rc.d/*.vim'), "\n")
"   exe 'source ' . s:path
" endfor

"---------------------------------------------------------------------------
set secure

" vim: foldmethod=marker
